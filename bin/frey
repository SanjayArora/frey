#!/usr/bin/env coffee
Frey  = require "../src/Frey"
debug = require("depurar")("frey")
yargs = require "yargs"

yargs
  .usage "Usage: frey <command> [options]"
  .example "frey backup -d ./envs/production", "backup platform described in ./envs/production"
  .options
    app:
      alias   : "a"
      default : "{directory}|basename"
      nargs   : 1
      type    : "string"
      describe: "Name of application for which we're creating infrastructure"
    recipe:
      alias   : "r"
      default : "{directory}/frey/production"
      nargs   : 1
      type    : "string"
      describe: "Directory that contains state, playbook & infra recipies"
    directory:
      alias   : "d"
      default : ->
        return process.cwd()
      nargs   : 1
      type    : "string"
      describe: "Root project directory that contains the .git"
    tools:
      alias   : "t"
      default : "{directory}/frey/tools"
      nargs   : 1
      type    : "string"
      describe: "Directory that contains the tools"
    tags:
      alias   : "g"
      nargs   : 1
      type    : "string"
      describe: "A list of tags to execute in isolation"
    sleep:
      alias   : "s"
      default : 5
      nargs   : 1
      type    : "number"
      describe: "Wait x seconds between showing infra plan, and executing it"
    bail:
      alias   : "b"
      boolean : true
      describe: "Do not follow the chain of commands"
    verbose:
      alias   : "v"
      count   : true
      describe: "Show debug info"
    unsafe:
      alias   : "u"
      boolean : true
      describe: "Allow execution, even though your Git working directory is unclean"
  .command "completion", "Install CLI auto completion"
  .epilog "Copyright 2015 Transloadit"
  .help "help"
  .wrap yargs.terminalWidth()
  .version ->
    require("../package").version

# First add chained commands, in order
done = []
for cmd in Frey.chain
  if cmd not in done
    description = Frey.commands[cmd]
    yargs.command cmd, description + " (chained)"
    done.push cmd

# Now add any remaining commands, not added already
for cmd, description of Frey.commands
  if cmd not in done
    yargs.command cmd, description
    done.push cmd

# 'Execute' yargs
argv = yargs.argv

# We override built-in completion command
if argv._?[0] == "completion"
  # we want to make sure we have the global /usr/local/bin/frey instead of
  # ../../../frey/bin/frey in the ~/.bash_profile
  argv.showCompletionScript process.env._
  process.exit 0

# Apply any rc config
config = require("rc")("frey", {}, argv)

# Instantiate Frey with config
frey = new Frey config

# Bombs away
frey.run (err) ->
  if err
    yargs.showHelp()
    console.error err
  else
    console.log "Done"
