#!/usr/bin/env coffee
Frey           = require "../src/Frey"
debug          = require("depurar")("frey")
yargs          = require "yargs"
updateNotifier = require "update-notifier"
pkg            = require "../package.json"
updateNotifier(pkg: pkg).notify(defer: false)

yargs
  .usage "Usage: frey <command> [options]"
  .example "frey backup -d ./envs/production", "backup platform described in ./envs/production"
  .options
    app:
      alias   : "a"
      default : "{cwd}|basename"
      nargs   : 1
      type    : "string"
      describe: "Name of application for which we're creating infrastructure"
    recipe:
      alias   : "r"
      default : "{cwd}/frey/production"
      nargs   : 1
      type    : "string"
      describe: "Directory that contains state and recipies"
    tools:
      alias   : "t"
      default : "{home}/.frey/tools"
      nargs   : 1
      type    : "string"
      describe: "Directory that contains the tools"
    "force-yes":
      alias   : "f"
      default : false
      boolean : true
      describe: "Answer yes to all questions (dangerous!)"
    tags:
      alias   : "g"
      nargs   : 1
      type    : "string"
      describe: "A list of tags to execute in isolation"
    sleep:
      alias   : "s"
      default : 5
      nargs   : 1
      type    : "number"
      describe: "Wait x seconds between showing infra plan, and executing it"
    bail:
      alias   : "b"
      boolean : true
      describe: "Do not follow the chain of commands, run a one-off command"
    "bail-after":
      nargs   : 1
      type    : "string"
      describe: "After running this command, abort the chain"
    "no-color":
      boolean : true
      describe: "Color support is detected, this forces colors off"
    verbose:
      alias   : "v"
      count   : true
      describe: "Show debug info"
    unsafe:
      alias   : "u"
      boolean : true
      describe: "Allow execution, even though your Git working directory is unclean"
  .command "completion", "Install CLI auto completion"
  .epilog "Copyright 2015 Transloadit"
  .help "help"
  .wrap yargs.terminalWidth()
  .version ->
    pkg.version

# First add chained commands, in order
done = []
for cmd in Frey.chain
  if cmd not in done
    description = Frey.commands[cmd]
    yargs.command cmd, description + " (chained)"
    done.push cmd

# Now add any remaining commands, not added already
for cmd, description of Frey.commands
  if cmd not in done
    yargs.command cmd, description
    done.push cmd

# 'Execute' yargs
argv = yargs.argv

# We override built-in completion command
if argv._?[0] == "completion"
  # we want to make sure we have the global /usr/local/bin/frey instead of
  # ../../../frey/bin/frey in the ~/.bash_profile
  argv.showCompletionScript process.env._
  process.exit 0

# Apply any rc options
options = require("rc")("frey", {}, argv)

# Instantiate Frey with options
frey = new Frey options

# Bombs away
frey.run (err) ->
  if err
    # yargs.showHelp()
    console.error ""
    console.error "--> Exiting with error: #{err.message}"
    if err.details?
      console.error "--> Details:"
      console.error ""
      console.error err.details
    console.error ""
    process.exit 1
  else
    console.log "Done"
